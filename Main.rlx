
#Require "$/Console.rlx"
#Require "$/AVLTree.rlx"
#Require "$/PointerArray.rlx"

#Require "Time.rlx"
#Require "fcntl.rlx"

#Require "AVXMemory.rlx"
#Require "SSE4Memory.rlx"

enum DiskErrorKind {
    Read,
    Write,
    Corruption
}

define i8* GetDiskErrorKindName(DiskErrorKind Kind) {
    i8*[3] Names {"Read", "Write", "Corruption"}

    return Names[Kind As i32]
}

struct ErrorRecord {
    i64 BlockNumber
    i64[3] ErrorCounts

    define i64 Count() {
        i64 ReadErrors := this->ErrorCounts[DiskErrorKind::Read]
        i64 WriteErrors := this->ErrorCounts[DiskErrorKind::Write]
        i64 CorruptionErrors := this->ErrorCounts[DiskErrorKind::Corruption]

        return ReadErrors + WriteErrors + CorruptionErrors
    }
}

struct PassInfo {
    i64 StartTime
    i64 EndTime

    i8* Name
    i32 Value

    i64 StartBlock
    i64 EndBlock

    AVLTree* Errors

    void(StorageDevice*, PassInfo*) DoPass

    static self* New(i8* Name, i32 Value, void(StorageDevice*, PassInfo*) DoPass) {
        self* this := Alloc(#self)

        this->Name := Name
        this->Value := Value
        this->DoPass := DoPass

        this->Errors := AVLTree:New(#ErrorRecord)

        return this
    }
}

struct StorageDevice {
    i8* FilePath
    i64 FileHandle

    i64 OutputHandle

    i64 BlockSize
    i64 TotalBlocks

    i64 BatchSize
    void* WriteBuffer
    void* ReadBuffer

    i64 StartTime
    i32 CurrentPassNumber

    i64 MaxErrors

    AVLTree* ErrorRecords
    ErrorRecord* GlobalErrors
    PointerArray* Passes ; <PassInfo*>

    static self* New() {
        self* this := Alloc(#self)

        this->ErrorRecords := AVLTree:New(#ErrorRecord)
        this->GlobalErrors := Alloc(#ErrorRecord)
        this->Passes := PointerArray:New()

        return this
    }

    define PassInfo* CurrentPass() {
        return this->Passes->At(this->CurrentPassNumber)
    }

    define void AddPass(PassInfo* Info) {
        this->Passes->Push(Info)
    }
}

define i64 GetUnixtime() {
    timeval val
    GetTimeOfDay(&val)

    return val.Seconds
}

define void DoBlockPrint(PrintOutputAdapterPrototype* this, i8* Format, i32* FormatIndex, void* Parameters, i32 ParameterCount, i32* ParameterIndex) {
	i64 Block := Parameters[*ParameterIndex] As i64
	
	i8[100] Buffer
	
    for (i32 Index := 0, Block != 0, Index += 1) {
        if (Index % 4 = 3) {
            Buffer[Index] := ','
        }
        else {
            Buffer[Index] := NumberToCharacter((Block % 10) As i8)
            Block /= 10
        }
    }

    if (Index = 0) {
		Buffer[0] := '0'
		Index := 1
	}

    MemoryReverse(Buffer, Index)

    this->DoPrintCharacters(Buffer, Index)
	
	*ParameterIndex += 1
}

PrintFormatPrototype BlockFormatPrototype {
	Name: "block",
	Format: &DoBlockPrint
}

i32 BLOCK_FORMAT_PROTOTYPE_ID := AddPrintFormat(&BlockFormatPrototype)

i64 LastStatusHash := 0

define void PrintStatus(StorageDevice* Device, i64 Block) {
    ; Calculate progress percentage with one decimal
    i64 Percent := (Block * 1000) / Device->TotalBlocks

    i64 Whole := Percent / 10
    i64 Fraction := Percent % 10

    ; Calculate time spent
    i64 Duration := GetUnixtime() - Device->StartTime

    ; Reduce output spam
    i64 Hash := Duration + (Percent << 32)

    if (Hash = LastStatusHash) {
        return
    }

    LastStatusHash := Hash

    ; Format to hours/minutes/seconds
    i64 Seconds := Duration % 60
    Duration /= 60
    i64 Hours := Duration / 60
    i64 Minutes := Duration % 60

    i8* SecondPadding := ""
    i8* MinutePadding := ""

    if (Seconds < 10) {
        SecondPadding := "0"
    }

    if (Minutes < 10) {
        MinutePadding := "0"
    }

    ; Dump everything
    Print("%c%s: %s: ", 0xD, Device->FilePath, Device->CurrentPass()->Name)
    Print("[%i:%s%i:%s%i]", Hours, MinutePadding, Minutes, SecondPadding, Seconds)
    Print(" %i.%i%%", Whole, Fraction)

    i64 ReadErrors := Device->GlobalErrors->ErrorCounts[DiskErrorKind::Read]
    i64 WriteErrors := Device->GlobalErrors->ErrorCounts[DiskErrorKind::Write]
    i64 CorruptionErrors := Device->GlobalErrors->ErrorCounts[DiskErrorKind::Corruption]

    Print(", %i errored blocks, %ir/%iw/%ic errors", Device->ErrorRecords->Count(), ReadErrors, WriteErrors, CorruptionErrors)
    Print(" [--resume %i %block]", Device->CurrentPassNumber, Block)
}

define void RecordBlockError(StorageDevice* Device, i64 Block, DiskErrorKind Kind) {
    ErrorRecord* Record := Device->ErrorRecords->Get(Block)

    if (Record) {
        Record->ErrorCounts[Kind] += 1
    }
    else {
        ErrorRecord NewRecord {
            BlockNumber: Block,
            ErrorCounts: {0, 0, 0}
        }

        NewRecord.ErrorCounts[Kind] += 1

        Device->ErrorRecords->Insert(Block, &NewRecord)
    }

    i8* KindName := GetDiskErrorKindName(Kind)

    i8[300] ErrorBuffer
    i32 Length := FormatString(ErrorBuffer, 300, "%s: %s: %s error on block %i (%block)\n", Device->FilePath, Device->CurrentPass()->Name, KindName, Block, Block)
    ErrorBuffer[Length] := 0

    Print("%s", ErrorBuffer)

    if (Device->OutputHandle != 0) {
        FileWrite(Device->OutputHandle, ErrorBuffer, Length)
    }

    Device->GlobalErrors->ErrorCounts[Kind] += 1

    if (Device->GlobalErrors->Count() >= Device->MaxErrors) {
        Print("Max error threshold reached")
        Exit(1)
    }
}

define void PopulateDeviceBuffers(StorageDevice* Device, i32 Value) {
    FastSetMemory8(Device->ReadBuffer, Value, Device->BatchSize * Device->BlockSize)
    FastSetMemory8(Device->WriteBuffer, Value, Device->BatchSize * Device->BlockSize)
}

define i64 WriteBlocks(StorageDevice* Device, i64 Block, i64 Count) {
    FileSeek(Device->FileHandle, (Block * Device->BlockSize) As i32, SEEK_SET)
    i64 Result := FileWrite(Device->FileHandle, Device->WriteBuffer, (Count * Device->BlockSize) As i32)

    if (Result < 0) {
        Result := 0
    }

    return Result / Device->BlockSize
}

define void WritePass(StorageDevice* Device, PassInfo* Pass) {
    PopulateDeviceBuffers(Device, Pass->Value)
    FileSeek(Device->FileHandle, 0, SEEK_SET)

    i64 Block := Pass->StartBlock
    i64 AttemptSize := Device->BatchSize
    i8 LastFailed := false

    while (Block != Pass->EndBlock) {
        if (Block + AttemptSize > Pass->EndBlock) {
            AttemptSize := Pass->EndBlock - Block
        }

        i64 Written := WriteBlocks(Device, Block, AttemptSize)

        if (Written = 0 && LastFailed) {
            RecordBlockError(Device, Block, DiskErrorKind::Write)
            Block += 1
        }
        else if (Written = AttemptSize) {
            Block += AttemptSize
            AttemptSize := Device->BatchSize
            LastFailed := false
        }
        else {
            AttemptSize := 1
            LastFailed := true
        }

        PrintStatus(Device, Block)
    }
}

define i64 ReadBlocks(StorageDevice* Device, i64 Block, i64 Count) {
    FileSeek(Device->FileHandle, (Block * Device->BlockSize) As i32, SEEK_SET)
    i64 Result := FileRead(Device->FileHandle, Device->ReadBuffer, (Count * Device->BlockSize) As i32)

    if (Result < 0) {
        Result := 0
    }

    return Result / Device->BlockSize
}

i8(i32, void*, i32) FAST_COMPARE_MEMORY := &SSE4CompareMemory

define void ReadPass(StorageDevice* Device, PassInfo* Pass) {
    PopulateDeviceBuffers(Device, Pass->Value)
    FileSeek(Device->FileHandle, 0, SEEK_SET)

    i64 Block := Pass->StartBlock
    i64 AttemptSize := Device->BatchSize
    i8 LastFailed := false

    while (Block != Pass->EndBlock) {
        if (Block + AttemptSize > Pass->EndBlock) {
            AttemptSize := Pass->EndBlock - Block
        }

        i64 Read := ReadBlocks(Device, Block, AttemptSize)

        if (Read = 0 && LastFailed) {
            RecordBlockError(Device, Block, DiskErrorKind::Read)
            Block += 1
        }
        else if (Read = AttemptSize) {
            if !(FAST_COMPARE_MEMORY(Pass->Value, Device->ReadBuffer, (AttemptSize * Device->BlockSize) As i32)) {
                for (i64 Failed := 0, Failed < AttemptSize, Failed++) {
                    RecordBlockError(Device, Block + Failed, DiskErrorKind::Corruption)
                }
            }

            Block += AttemptSize
            AttemptSize := Device->BatchSize
            LastFailed := false
        }
        else {
            AttemptSize := 1
            LastFailed := true
        }

        PrintStatus(Device, Block)
    }
}

define i64 BlockAToI(i8* String, i32 Length, i64 Base) {
	i64 Result := 0
	i64 Sign := 1
	i32 Start := 0
	
	if !(IsNumeric(String[Start], Base)) {
		return 0
	}
		
	for (i32 Index := Start, Index < Length, Index += 1) {
		i8 NextCharacter := String[Index]

        if (NextCharacter = ',') {
            continue
        }
		
		if !(IsNumeric(NextCharacter, Base)) {
			Break
		}
		
		Result *= Base
		Result += GetCharacterValue(NextCharacter)
	}
	
	return Result * Sign
}
define i64 BlockAToI(i8* String, i64 Base) {
	return BlockAToI(String, StringLength(String), Base)
}
define i64 BlockAToI(i8* String) {
	return BlockAToI(String, StringLength(String), 10)
}

define void Main(i32 ArgC, i8** ArgV) {
    if (IsAVXSupported()) {
        Print("AVX support detected, using 256 bit comparisons")

        FAST_COMPARE_MEMORY := &AVXCompareMemory
    }

    StorageDevice* Device := StorageDevice:New()

    Device->BlockSize := 512
    Device->BatchSize := 8192

    Device->MaxErrors := 1000

    i32 FirstPass := 0
    
    i32[4] DefaultPattern {0xAA, 0x55, 0xFF, 0x00}

    for (i32 Index := 0, Index < 4, Index++) {
        i32 Value := DefaultPattern[Index]

        Device->AddPass(PassInfo:New(FormatString("Write %B", Value), Value, &WritePass))
        Device->AddPass(PassInfo:New(FormatString("Read  %B", Value), Value, &ReadPass))
    }

    for (i32 Index := 1, Index < ArgC, Index++) {
        i8* Argument := ArgV[Index]

        if (StringEquals(Argument, "-b") || StringEquals(Argument, "--block-size")) {
            Device->BlockSize := AToI(ArgV[++Index])
        }
        else if (StringEquals(Argument, "-s") || StringEquals(Argument, "--size")) {
            Device->TotalBlocks := BlockAToI(ArgV[++Index])
        }
        else if (StringEquals(Argument, "-p") || StringEquals(Argument, "--batch-size")) {
            Device->BatchSize := BlockAToI(ArgV[++Index])
        }
        else if (StringEquals(Argument, "-e") || StringEquals(Argument, "--max-errors")) {
            Device->MaxErrors := BlockAToI(ArgV[++Index])
        }
        else if (StringEquals(Argument, "-r") || StringEquals(Argument, "--resume")) {
            FirstPass := AToI(ArgV[++Index]) As i32

            PassInfo* FirstPassInfo := Device->Passes->At(FirstPass)
            FirstPassInfo->StartBlock := BlockAToI(ArgV[++Index])
        }
        else if (StringEquals(Argument, "-d") || StringEquals(Argument, "--device")) {
            Device->FilePath := ArgV[++Index]
            Device->FileHandle := FileOpen(Device->FilePath, FILE_READ_WRITE | O_DIRECT)

            i64 OldFlags := fcntl(Device->FileHandle, F_GETFL, 0)
            fcntl(Device->FileHandle, F_SETFL, OldFlags | O_DIRECT)
        }
        else if (StringEquals(Argument, "-o") || StringEquals(Argument, "--output-file")) {
            Device->OutputHandle := FileOpen(ArgV[++Index], FILE_READ_WRITE | FILE_CREATE_NEW)

            FileSeek(Device->OutputHandle, 0, SEEK_END)
        }
    }

    if (Device->FilePath = null) {
        Print("No device specified")
        return 1
    }
    else if (Device->TotalBlocks = 0) {
        Print("No device size specified")
        return 1
    }

    Device->WriteBuffer := Alloc(Device->BatchSize * Device->BlockSize)
    Device->ReadBuffer := Alloc(Device->BatchSize * Device->BlockSize)

    timeval Now
    GetTimeOfDay(&Now)
    Device->StartTime := Now.Seconds

    Print("Device: %s\n", Device->FilePath)
    Print("Total Blocks: %block\n", Device->TotalBlocks)
    Print("Total Bytes: %i\n", Device->TotalBlocks * Device->BlockSize)
    Print("Testing %block blocks at a time\n", Device->BatchSize)
    Print("Pass Summary:\n")

    i32 Count := Device->Passes->Count()

    for (i32 Index := FirstPass, Index < Count, Index++) {
        PassInfo* Pass := Device->Passes->At(Index)

        if (Pass->EndBlock = 0) {
            Pass->EndBlock := Device->TotalBlocks
        }

        Print("  Pass %i: %s (%block - %block)\n", Index, Pass->Name, Pass->StartBlock, Pass->EndBlock)
    }

    for (i32 Index := FirstPass, Index < 8, Index++) {
        Device->CurrentPassNumber := Index

        PassInfo* Pass := Device->Passes->At(Index)

        Print("Starting Pass %i: %s (%block - %block)\n", Index, Pass->Name, Pass->StartBlock, Pass->EndBlock)

        Pass->DoPass(Device, Pass)
        Print("\n")
    }

    /*
    loop {
        Sleep(1)
        
        timeval Now
        GetTimeOfDay(&Now)

        i64 Progress := Now.Seconds - Start
        
        PrintStatus(Device, Progress)

        
    }*/

    Print("hello\n")
}